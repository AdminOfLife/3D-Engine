precision highp float;

#pragma optimize(off)
#pragma debug(on)

varying vec3 fPosition_view;
varying vec3 fNormal_view;
varying vec3 fNormal;
varying mat4 fWorldToView;

vec3 lightPos[2];
vec3 lightColor[2];

uniform float time;

vec3 normal_view;

struct Light {

    vec3 pos;
    vec3 vector; // Vecteur du fragment évalué à la source de lumière
    vec3 color;
    float intensity;
};

float calcAttenuation(vec3 lightVec);
vec3 computeDiffuse(Light light);
vec3 computeSpecular(Light light);
Light computePointLight(vec3 lightPos, vec3 color);
Light computeDirectionalLight(vec3 vector, vec3 color);

void main() {
    
    lightPos[0] = vec3(cos(time*10.), 0.0, 1.5);
    lightColor[0] = vec3(1., 0, 0);
    lightPos[1] = vec3(-1, -cos(time*10.1), -1.);
    lightColor[1] = vec3(0, 0, 1);
    
    vec3 diffColor = vec3(0);
    vec3 specColor = vec3(0);

    normal_view = normalize(fNormal_view);

    for (int i = 0; i < 2 ; i++)
    {
        //Light light = computeDirectionalLight(lightPos[i], lightColor[i]);
        Light light = computePointLight(lightPos[i], lightColor[i]);

        diffColor += computeDiffuse(light);
        specColor += computeSpecular(light);
    }

    gl_FragColor = vec4(diffColor + specColor, 1);

}

#define ATTEN_CONST 1.
#define ATTEN_LINEAR 0.1
#define ATTEN_QUADRA 0.5

float calcAttenuation(vec3 lightVec) {

    float distance = length(lightVec);
//    if (distance > 200)
//        return 0.;
    return 1. / (ATTEN_CONST +
                (ATTEN_LINEAR * distance) +
                (ATTEN_QUADRA * distance * distance) );

}

Light computeDirectionalLight(vec3 vector, vec3 color) {

    Light direcLight;
    direcLight.pos = vector; // En théorie à l'infini, donc a vérifier cette ligne
    direcLight.vector = normalize(vector); // Lampe à l'infini
    direcLight.color = color;
    direcLight.intensity = 1.;
    return direcLight;
}

Light computePointLight(vec3 lightPos, vec3 color) {

    lightPos = vec3(fWorldToView * vec4(lightPos, 1));

    Light pointLight;
    pointLight.pos = lightPos;
    pointLight.color = color;
    pointLight.vector = normalize(lightPos - fPosition_view);
    pointLight.intensity = calcAttenuation(lightPos - fPosition_view);
    return pointLight;
}

vec3 computeDiffuse(Light light) {

    return light.color * (light.intensity * max(dot(light.vector, normal_view), 0.));
}

vec3 computeSpecular(Light light) {

//    vec3 viewVector = normalize(-camPosition.xyz);
//    vec3 reflectedLight = 2.0 * dot(light.vector, normal) * normal - light.vector;

//    vec4 camPosition = normalize(fWorldToView * vec4(position, 1));
//    vec4 reflectedLight = reflect(-normalize(fWorldToView * vec4(light.pos, 1) - camPosition), normalize(vec4(transpose(inverse(mat3(fWorldToView))) * normal, 0)));
    vec3 reflectedLight = reflect(-normalize(light.pos - fPosition_view), normal_view);
    if (dot(normal_view, light.vector) <= 0.0)
        return vec3(0);
    else
        return light.intensity * light.color * pow( max( dot(normalize(reflectedLight), normalize(-fPosition_view)), 0.), 15.);

}
